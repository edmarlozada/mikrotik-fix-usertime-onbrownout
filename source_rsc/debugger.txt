
# eUpTimeBackup #
{
local iName "hs-UpTimeSaved"; local x 5;
if ([/system script find name=$iName]="") do={
  /system script add name=$iName comment="( hotspot: Saved Uptime )"
  while (($x>0) and ([/system script find name=$iName]="")) do={set x ($x-1);delay 1s}
}
if ($x>0) do={
  local iData ":local tData [:toarray \"\"];\r\n"
  if ([len [/ip hotspot active print as-value]]>0) do={
    /system scheduler set [find name=eUpTimeBackup] disabled=yes
    /system scheduler set [find name=eUpTimeUpdate] interval=0
    /system logging set [find topics=critical] disabled=no
    foreach au in=[/ip hotspot active find] do={
      local aUser [/ip hotspot active get $au user]
      local uTimeLimit [/ip hotspot user get [find name=$aUser] limit-uptime]
      if ($uTimeLimit>0) do={
        local aTimeUseBak [/ip hotspot active get $au uptime]
        local aTimeLftBak [/ip hotspot active get $au session-time-left]
        local uTimeUseBak [/ip hotspot user get [find name=$aUser] uptime]
        put "( UpTimeBackup ) => aUser:[$aUser] uTimeUseBak:[$uTimeUseBak] aTimeUseBak:[$aTimeUseBak]"
        set iData "$iData :set (\$tData->[:len \$tData]) { \"$aUser\"; $uTimeUseBak; $aTimeUseBak; $aTimeLftBak; $uTimeLimit }\r\n"
      }
    }
    /system scheduler set [find name=eUpTimeBackup] disabled=no
  }
  set iData ("$iData".":return \$tData\r\n")
  /system script set [find name=$iName] source=$iData
}
}


# eUpTimeUpdate #
{
local iName "hs-UpTimeSaved"
if ([/system script find name=$iName]!="") do={
  log info ("eUpTimeUpdate Begin => $[/system clock get time]")
  /system scheduler set [find name=eUpTimeBackup] disabled=yes
  do {
    local iData [[parse [/system script get [find name=$iName] source]]]
    foreach au in=$iData do={
      local aUser ($au->0); local uTimeUseBak ($au->1); local aTimeUseBak ($au->2);
      local uTimeLimNow [/ip hotspot user get [find name=$aUser] limit-uptime]
      local uTimeUseNow [/ip hotspot user get [find name=$aUser] uptime]
      local uTimeLimNew ([totime $uTimeLimNow]-[totime $aTimeUseBak])
      put "( UpTimeUpdate ) Bak => aUser:[$aUser] uTimeUseBak:[$uTimeUseBak] aTimeUseBak:[$aTimeUseBak]"
      put "( UpTimeUpdate ) Now => aUser:[$aUser] uTimeUseNow:[$uTimeUseNow] uTimeLimNow:[$uTimeLimNow]"
      if ($uTimeUseNow=$uTimeUseBak) do={
        put "( UpTimeUpdate ) NEW => New-Limit-Uptime:[$uTimeLimNew]"
        #/ip hotspot user set [find name=$aUser] limit-uptime=$uTimeLimNew
      }
    }
  } on-error={ log warning "ERROR: invalid saved data" } 
  /system scheduler set [find name=eUpTimeBackup] disabled=no
  log info ("eUpTimeUpdate End => $[/system clock get time]")
}
}

